# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=curl
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.44.0
pkgrel=3
pkgdesc="An URL retrival utility and library. (mingw-w64)"
arch=('any')
url="http://curl.haxx.se"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-zlib"
	 "${MINGW_PACKAGE_PREFIX}-ca-certificates"
         "${MINGW_PACKAGE_PREFIX}-openssl")
options=('staticlibs')
source=("$url/download/${_realname}-${pkgver}.tar.bz2"
        "0001-curl-relocation.patch"
        "0002-curl-mingw-enable-static.patch")
md5sums=('6b952ca00e5473b16a11f05f06aa8dae'
         '58520051c4ed77781d233c3fa40a5435'
         'eac9e212e619490966ae47004bec547b')

prepare() {
  cd "${_realname}-${pkgver}"
  rm -f lib/pathtools.h lib/pathtools.c > /dev/null 2>&1 || true
  patch -p1 -i "${srcdir}/0001-curl-relocation.patch"
  patch -p1 -i "${srcdir}/0002-curl-mingw-enable-static.patch"
  autoreconf -vfi
}

build() {
  local -a extra_config
  if check_option "debug" "y"; then
    extra_config+=( --enable-debug )
  else
    extra_config+=( --disable-debug )
  fi
  mkdir -p "${srcdir}/build-${CARCH}"

  cd "${srcdir}/build-${CARCH}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-random \
    --enable-static \
    --enable-shared \
    --with-ssl \
    --without-libidn \
    --without-winidn \
    --without-librtmp \
    --without-libssh2 \
    --with-ca-bundle="${MINGW_PREFIX}/ssl/certs/ca-bundle.crt" \
    --without-axtls \
    --without-nss \
    --without-polarssl \
    --without-gnutls \
    --without-winssl \
    --disable-ldaps \
    --disable-ldap \
    --disable-rtsp \
    --disable-ares \
    --without-nghttp2 \
    "${extra_config[@]}"
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
